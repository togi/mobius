apply plugin: 'com.android.library'


apply from: rootProject.file('gradle/java_8_sources.gradle')

ext.compileUnitTestsAsJava8()

repositories {
    google()
}

dependencies {
    api project(':mobius-core')
    api project(':mobius-extras')

    implementation "org.slf4j:slf4j-api:${versions.slf4j}"
    implementation "com.google.code.findbugs:jsr305:${versions.jsr305}"
}

static def gitSha() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        buildConfigField "String", "LIBRARY_VERSION", "\"${VERSION_NAME}\""
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
